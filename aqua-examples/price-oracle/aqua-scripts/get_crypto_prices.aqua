service PriceGetter("service-id"):
    price_getter: string, string -> string

data Payload:
    coin: string
    currency: string

data NodeServicePair:
    node: string
    service_id: string


data BigPayload:
    coin: string
    currency: string
    node: string
    service_id: string


func get_price(coin: string, currency: string, node: string, service_id: string) -> string:
    on node:
        PriceGetter service_id
        price <- PriceGetter.price_getter(coin, currency)
    <- price


func get_prices_seq(payloads: []Payload, ns_pairs: []NodeServicePair) -> *string:
    prices: *string
    for p <- ns_pairs:
        on p.node:
            for d <- payloads:
                PriceGetter p.service_id
                prices <- PriceGetter.price_getter(d.coin, d.currency)
    <- prices

func get_prices_par(payload: []Payload, ns_pairs: []NodeServicePair) -> *string:
    prices: *string
    for p <- ns_pairs par:
        PriceGetter p.service_id
        on p.node:
            for d <- payload:
                prices <- PriceGetter.price_getter(d.coin, d.currency)
    <- prices


func get_prices_par_2(payload: []BigPayload) -> *string:
    prices: *string
    for p <- payload par:
        on p.node:
            PriceGetter p.service_id
            prices <- PriceGetter.price_getter(p.coin, p.currency)
    <- prices